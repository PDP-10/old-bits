
	TITLE SYSLOD

COMMENT	!
	BARE-MACHINE PDP-10 SYSTEM LOADER
	FOR SEVERAL SYS VERSIONS
	(25 SEPT 1969).../BLT
	CHANGED TO USE NEW DISK CODE
	24 OCTOBER 1971 /RON
	NOW NEWER VERSION FOR SYSTEMS CONCEPTS DISK CHANNEL	
	13 OCTOBER 1972 /RON
	EVEN NEWER VERSION FOR KI-10, DIFFERENT SYSTEM LAYOUT
	23 JANUARY 1975 /RON
	WHOLE FORMAT REVISED FOR NEW AMPEX DISKS
	22 JUNE 1975 /RON
	!

NEWDSK __ 00		;FROR NEW SYSTEMS CONCEPTS DISK CODE
DINTSW __ 00		;NON -0 FOR DSK INTERUPT STUFF
RIMSW__00
DDTSW__00

MESBUF _ 276000		;BASE FOR PAGE 0

IFN RIMSW,<
	EXTERNAL RIM10B
	EXTERNAL RIMCON
	>

	APR __ 0
	PI __ 4

EXTERNAL TYPEIT, TYOCT, GETCHR, PUTDSK, GETDSK
INTERNAL PRGBEG, PRGLOC, GOGO, DSKCHL
INTERNAL DSKCHN, DSKON, DSKOFF, PION, PIOFF

DSKCHN__4
DSKON__2010
DSKOFF_1010
PION__200
PIOFF__400

A _ 1
B _ 2
C _ 3
D _ 4

CNT __ 10
IDX __ 11
ADR __ 12
PAG __ 13


;HERE THE BOUNDS OF VERSION NUMBERS IS DEFINED

LOVER__1		;LOWEST VERSION NUMBER
HIVER__6		;HIGHEST NUMBER EXCEPTED, DEFAULT IF TOO BIG

PG0SAV__MESBUF+177		;SAVE POINTERS TO PAGE 0 STUFF
PG0PNT__MESBUF+200		;SAVING 70-107

PAGTBL__MESBUF+400		;TABLE FOR PSEUDO-MAP OF SYSTEM SAVE
			;HAS PAGE NUM IN LH, CHECKSUM IN RH
			;OFFSET BY PAGE NUMBER OF SAVE
sysVTM__mesbuf+771		;DATE/TIME OF SYSTEM SAVE...
SYMSAV__MESBUF+772		;SYMBOL POINTER SAVE
syseg2__mesbuf+773		;OFFSET TO BEGINNING OF SWAPPABLE MON
systrt__mesbuf+774		;STARTING ADR
SYSPAG__MESBUF+775		;NUMBER OF SAVED PAGES...
syscnt__mesbuf+776		;NUMBER OF DISK AREAS
sAVCSM__mesbuf+777		;CHECKSUM FOR REST OF PAGE 0.

RECCYL__=190			;NUMBEROF RECORDS/CYLINDER
NCYL__2				;NUMBER OF CYLINDERS/SAVE

SAVSIZ__NCYL*RECCYL		;NUMBER OF RECORDS/SAVE
SAVOFF__RECCYL			;ALSO DON'T USE CYLINDER 0



;STARTS HERE...

PRGLOC:
PRGBEG:
GOGO:
IFN DDTSW,<		EXTERNAL DDTX	;DO WE WNAT DDT??
	CAIA ;;;;; JSR	SYMPUT		;GOOD TYPE START, RESTORE SYMPNT
	 JRST	DDTX		;DDT GETTER
>
	MOVEI 16, 600000	;WAIT 200 MS, FOR READER TO STOP...
	SOJG  16, .		;BEFORE TRYING TO START IT AGAIN.

	CONO 104, 60		;START PAPER TAPE READER
	MOVEI 16, 400000	;ABOUT 150 MS, TAKES 50 TO GET STARTED..
	SOJG  16, .		;TO MOVE TAPE PAST READIN...

	CONO APR, 200000	;I/O RESET, WILL STOP PTR TOO...
	CONO PI, 711577		;CLEAR MACHINE	
	CONO APR, 525300	;KI TYPE APR CLEAR, TOO


ASKV:	MOVEI	B, [ASCIZ &

KI-10 LOADER - SYSTEM VERSION : &]
	JSR	TYPEIT		;ASK VERSION NUMBER

	JSR	GETCHR		;GET A CHARACTER
	MOVE	17, C	
	SUBI	17, 60		;MAKE INTO NUMBER
	CAIL	17, LOVER	;NO NEGATIVE	
	CAILE	17, HIVER		
	 JRST	ASKV	 

	SETZM	DSKBLK			;SET MODULE NUMBER 0
	MOVE  16, [XWD -1000, MESBUF]
	MOVEM 16, DSKBLK+2		;AND WCMA
	MOVEM 17, SYSVER		;SAVE VERSION NUMBER
	IMULI 17, SAVSIZ		;GET BASE RECORD NUMBER
	ADDI 17, SAVOFF			;PLUS OFFSET
	MOVEM 17, BASREC		;STORE IN GOOD PLACE
	MOVEM 17, DSKBLK+1		;FOR PAGE 0 READ

IFN DDTSW,< MOVE 16, 36		;SAVE SYMBOLS IF DEBUGGING
	MOVEM 16, DDTSYM >

	MOVE	17, [XWD 20, 21]	;SET TO ZERO CORE
	SETZM	20
	BLT	17, PRGLOC-1		;RIGHT TO THE TIP OF OUR PROG

IFN DDTSW,<	MOVE 16, DDTSYM
	MOVEM 16, 36 >

EXTERNAL DSKINI

IFE NEWDSK,<	JSR DSKINI>		;RESET DISK
IFN NEWDSK,<	MOVE	17, [XWD -10, REC1]
		PUSHJ	17, DSKINI>

IFN DINTSW,<
	MOVE 17, [JSR DSKCHL]	;SET UP INTERUPT LOC	
	MOVEM 17, 40+2*DSKCHN
	CONO	PI, PION ! DSKON	;ENABLE INTERUPTS
>
     
 

LOADER:
	MOVEI 16, DSKBLK
	JSP 17, GETDSK		;GET PAGE ONE
	 HALT GOGO		;ERROR HALT

	MOVEI	B, [ASCIZ /

/]
	JSR	TYPEIT
	MOVEI B, MESBUF
	JSR	TYPEIT		;TYPE REC 1 MESSAGE


	HRLI	17, PG0PNT	;GET PAGE 0 POINTER
	HRR	17, PG0SAV	;AND WHERE IT GOES
	HLRZ	16, PG0SAV	;NOW GET COUNT
	ADDI	16, -1(17)	;CHANGE OFFSET
	BLT	17, (16)	;AND MOVE STUFF

	MOVE	16, SYMSAV	;GET SYMBOL POINTER
	MOVEM	16, 36		;INTO 36

	MOVEI	IDX, 1		;NOW GET/CHECK SYSTEM, STARTING PAGE 1
PAGLUP:	HLRZ	PAG, PAGTBL(IDX)	;GET PAGE NUMBER
	JUMPE	PAG, LUPDON		;IF NONE, THEN THRU!!
	MOVEI	ADR, (PAG)		;GET PAGE
	LSH	ADR, 11			;MAKE ADR
	HRLI	ADR, -1000		;MAKE WCMA, AOBJN
	MOVEM	ADR, DSKBLK+2
	MOVEI	16, (IDX)		;GET DISK PAGE NO.
	LSH	16, 1			;MAKE RECORD NUMBER
	ADD	16, BASREC		;ADD BASE
	MOVEM	16, DSKBLK+1		;INTO COMMAND BLOCK
	MOVEI	16, DSKBLK		;SET TO READ
	JSP	17, GETDSK
	 HALT	GOGO
	SETZ	CNT,			;CLEAR COUNT
CHKLUP:	ADD	CNT, (ADR)		;DO CHECKSUM
	AOBJN	ADR, CHKLUP
	HLRZ	16, CNT			;NOW FOLD IT
	ADDI	16, (CNT)
	TLNE	16, -1
	 AOJ	16,
	HRRZ	ADR, PAGTBL(IDX)	;GET OLD CHECKSUM
	CAIE	ADR, (16)		;COMPARE
	 JRST	KORERR			;LOZE...
	AOJA	IDX, PAGLUP		;ELSE NEXT PAGE...


LUPDON:	MOVEI	B, [ASCIZ /

GO!
/]
	JSR	TYPEIT
	
	SKIPN	12, SYSTRT		;GET STARTING ADR
	 MOVEI	12, 107			;ELSE USE 107
	CONI	APR, 17
	TLNN	17, 1			;SENSE SWITCH 6 FOR DDT GO?
	 JRST	NODDT
	MOVEI	B, [ASCIZ /  TO DDT...
/]
	JSR	TYPEIT
	MOVEI	12, 100			;START AT 100 FOR DDT
	
NODDT:	MOVE	11, [ JRST (12)]
	MOVE	10, [ CONO APR, 525300 ]	;RESET IF NXM
	MOVE	7, [ BLT 17, 777777 ]	;CLEAR ALL OF CORE
	MOVE	17, [XWD GOGO, GOGO+1]	;CLEAR ALL FROM PROG UP...
	SETZM	GOGO
	JRST	7			;GO TO BLT, THEN JRST (12)


KPAR:	MOVEI B, [ASCIZ /CORE PARITY ERROR
/]
	JSR TYPEIT
	HALT GOGO	;STOP HERE, BUT CONTINUE PERMITTED
			;CONTINUE AGAIN TO START SYSTEM

KORERR:	MOVEI B, [ASCIZ /CHECKSUM ERROR, PAGE /]
	JSR TYPEIT
	MOVEI	B, (PAG)
	SETZ	D,
	JSR TYOCT
	CONSZ PI, 200000
	 JRST KPAR	;PARITY BAD ALSO...
	HALT CHKLUP	;CONTINUE TO TRY AGAIN


DSKBLK:	BLOCK	3			;FOR DISK READIN

SYSVER:	0			;REMEMBER VERSION
BASREC:	0			;AND BASE RECORD....
	
IFN DINTSW,<
EXTERNAL DSKSV>
INTERNAL DSKRET

SAV.AC:	BLOCK 5		;SAVE ACS

DSKCHL:	0		;INTERUPT COMES JSR'ING TO HERE
	MOVEM	4, SAV.AC+4	;SAVE ACS
	MOVEI	4, SAV.AC
	BLT	4, SAV.AC+3
IFN DINTSW,<
	JSYS	DSKSV>		;TAKE DISK INTERUPT

DSKRET:	MOVSI	4, SAV.AC	;RESTORE ACS
	BLT	4, 3
	MOVE	4, SAV.AC+4
	JEN	@DSKCHL


INTERNAL DSKPER
DSKPER:	MOVEI	B, [ASCIZ /
DISK SENSED CORE PARITY ERROR!!
/]
	JSR	TYPEIT		;TYPE IT
	HALT	GOGO		;TRY AGAIN MAYBE??

IFN NEWDSK,<
INTERNAL TODCLK
TODCLK:	0
>

IFN DDTSW,<
SYMPUT:	0		;FOR JSR

	AOS	SYMPUT	;SKIPS....

	MOVE 16, SYMPNT
	MOVEM 16, 36
	JRST @SYMPUT

DDTSYM:	MOVE 16, 116
	MOVEM 16, SYMPNT
	HALTF

SYMPNT:	0

LODPAT:	BLOCK	100

>

END

